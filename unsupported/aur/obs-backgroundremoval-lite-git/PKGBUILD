# Maintainer: Kaito Udagawa <umireon at kaito dot tokyo>
pkgname=obs-backgroundremoval-lite-git
_pkgname="${pkgname%-git}"
pkgver=1.4.3
pkgrel=1
pkgdesc='Background Removal Lite for OBS Studio'
arch=(x86_64)
url="https://github.com/kaito-tokyo/${_pkgname}"
license=('GPL2')
depends=('obs-studio' 'fmt' 'ncnn')
makedepends=('git' 'cmake' 'ninja')
source=("${_pkgname}::git+https://github.com/kaito-tokyo/${_pkgname}.git#branch=add-pkgbuild")
sha256sums=('SKIP')


pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}


build() {
  cd "${_pkgname}"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  cmake --build build
}

package() {
  cd "${_pkgname}"
  DESTDIR="$pkgdir" cmake --install build
}
