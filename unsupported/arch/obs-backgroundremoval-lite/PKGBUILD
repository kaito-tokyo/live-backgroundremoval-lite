# Maintainer: Kaito Udagawa <umireon at kaito dot tokyo>
pkgname=obs-backgroundremoval-lite
pkgver=1.5.1
pkgrel=1
pkgdesc='Background Removal Lite for OBS Studio'
arch=('x86_64')
url="https://github.com/kaito-tokyo/${pkgname}"
license=('GPL-2.0-only')
depends=('obs-studio' 'fmt' 'ncnn')
makedepends=('git' 'cmake' 'ninja')
conflicts=("${pkgname}-git" "${pkgname}-git-debug")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kaito-tokyo/$pkgname/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('91f7230303cfe4f0661f3e5bab15f54ce6999fb7989d1fc5d69fe22c321b0764')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_FRONTEND_API=ON \
    -DENABLE_QT=ON \
    -GNinja
  cmake --build build
}

package() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="$pkgdir" cmake --install build
}
